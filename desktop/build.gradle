plugins {
    id 'scala'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileScala {
    scalaCompileOptions.deprecation = true
    scalaCompileOptions.unchecked = true
    scalaCompileOptions.optimize = false
    scalaCompileOptions.additionalParameters = [
            '-target:jvm-1.8',
            '-feature',
            '-language:higherKinds',
            '-language:existentials',
            '-encoding', 'UTF-8',
            '-opt:l:inline',
            '-opt-inline-from:**',
            '-opt-warnings:none',
            '-Ybackend-parallelism', '4',
            '-Xlint:-unused',
            '-Ywarn-unused:imports',
            '-Xfatal-warnings'
    ]
}

dependencies {
    implementation 'org.scala-lang:scala-library:2.13.8'
}

sourceSets.main.scala.srcDirs = ["src/"]
sourceSets.main.resources.srcDirs = ["../assets"]

project.ext.mainClassName = "org.eamonn.wfc.DesktopLauncher"
project.ext.assetsDir = new File("../assets")

import org.gradle.internal.os.OperatingSystem

task run(dependsOn: classes, type: JavaExec) {
    mainClass = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true

    if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
        // Required to run on macOS
        jvmArgs += "-XstartOnFirstThread"
    }
}

task debug(dependsOn: classes, type: JavaExec) {
    mainClass = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
    debug = true
}

task dist(type: Jar) {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}


dist.dependsOn classes

eclipse.project.name = appName + "-desktop"
